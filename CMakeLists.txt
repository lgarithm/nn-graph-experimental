CMAKE_MINIMUM_REQUIRED(VERSION 3.9)
PROJECT(nn-graph)

OPTION(USE_STRICT "Enable strict build options." OFF)
IF(USE_STRICT)
    ADD_DEFINITIONS(-Wall)
    IF(APPLE)
        ADD_DEFINITIONS(-Werror)
    ENDIF()
    ADD_DEFINITIONS(-Wfatal-errors)
ENDIF()

SET(CMAKE_CXX_STANDARD 17)

OPTION(USE_CLANG_TIDY "Use clang-tidy." OFF)
IF(USE_CLANG_TIDY)
    FIND_PROGRAM(CLANG_TIDY_EXE NAMES "clang-tidy")
    IF(CLANG_TIDY_EXE)
        MESSAGE(${CLANG_TIDY_EXE})
        SET(CMAKE_CXX_CLANG_TIDY clang-tidy)
    ENDIF()
ENDIF()

SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)

INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/include)

FIND_PACKAGE(stdtensor REQUIRED)
FIND_PACKAGE(stdnn-ops REQUIRED)

OPTION(USE_OPENBLAS "Build with blas." OFF)
SET(BLAS_HOME "" CACHE STRING "blas install dir")

IF(USE_OPENBLAS)
    MESSAGE("Using OpenBLAS")
    ADD_DEFINITIONS(-DSTDNN_OPS_HAVE_CBLAS)
    IF(APPLE)
        FIND_PACKAGE(OpenBLAS REQUIRED)
        INCLUDE_DIRECTORIES($ENV{HOME}/local/openblas/include)
        LINK_DIRECTORIES($ENV{HOME}/local/openblas/lib)
    ELSEIF(BLAS_HOME)
        INCLUDE_DIRECTORIES(${BLAS_HOME}/include)
        LINK_DIRECTORIES(${BLAS_HOME}/lib)
    ELSE()
        # https://cmake.org/cmake/help/v3.0/module/FindBLAS.html
        SET(BLA_VENDOR "OpenBLAS")
        FIND_PACKAGE(BLAS REQUIRED)
        INCLUDE_DIRECTORIES(${OpenBLAS_INCLUDE_DIRS})
    ENDIF()
ENDIF()

IF(${HAVE_CUDA})

ELSE()
    ADD_DEFINITIONS(-DUSE_FAKE_CUDA_RUNTIME=1)
ENDIF()

OPTION(BUILD_EXAMPLES "Build examples." OFF)
OPTION(BUILD_GPU_EXAMPLES "Build gpu examples." OFF)

IF(BUILD_EXAMPLES)
    FIND_PACKAGE(stdtracer REQUIRED)

    ADD_LIBRARY(trace ${CMAKE_SOURCE_DIR}/examples/trace.cpp)
    TARGET_USE_STDTRACER(trace)

    FUNCTION(ADD_EXAMPLE target)
        ADD_EXECUTABLE(${target} ${ARGN})
        TARGET_USE_STDTENSOR(${target})
        TARGET_USE_STDNN_OPS(${target})
        TARGET_LINK_LIBRARIES(${target} trace)
        IF(${HAVE_CUDA})
            TARGET_LINK_LIBRARIES(${target} cudart)
        ENDIF()
        IF(USE_OPENBLAS)
            IF(APPLE)
                TARGET_LINK_LIBRARIES(${target} openblas)
            ELSEIF(BLAS_HOME)
                TARGET_LINK_LIBRARIES(${target} openblas)
            ELSE()
                TARGET_LINK_LIBRARIES(${target} ${OpenBLAS_LIBRARIES})
            ENDIF()
        ENDIF()
    ENDFUNCTION()

    FUNCTION(SIMPLE_EXAMPLE target)
        ADD_EXAMPLE(${target} examples/${target}.cpp)
    ENDFUNCTION()

    SIMPLE_EXAMPLE(a-plus-b)
    SIMPLE_EXAMPLE(quadratic-function)
    SIMPLE_EXAMPLE(composite-derivative)
    SIMPLE_EXAMPLE(simple-derivative)
    SIMPLE_EXAMPLE(train-slp)
    SIMPLE_EXAMPLE(train-cnn)
    SIMPLE_EXAMPLE(mnist-dataset)

    IF(BUILD_GPU_EXAMPLES)
        ENABLE_LANGUAGE(CUDA)
        ADD_LIBRARY(cu-ops ${CMAKE_SOURCE_DIR}/src/nn/ops/example_gpu_ops.cu)
        SIMPLE_EXAMPLE(gpu-example)
        TARGET_LINK_LIBRARIES(gpu-example cu-ops)
    ENDIF()
ENDIF()

OPTION(BUILD_TESTS "Build tests." ON)

IF(BUILD_TESTS)
    ENABLE_TESTING()
    INCLUDE(cmake/tests.cmake)
ENDIF()
